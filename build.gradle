// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.3'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

android {
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']

            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']

            renderscript.srcDirs = ['src']

            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        androidTest.setRoot('tests')
    }

    signingConfigs {
    }
    compileSdkVersion 21
    buildToolsVersion "21.1.2"
    defaultConfig {
        applicationId "com.wifiafterconnect"
        minSdkVersion 14
        targetSdkVersion 21
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:21.0.3'
    compile files('libs/jsoup-1.7.2.jar')
    testCompile 'junit:junit:4.12'
}

allprojects {
    repositories {
        jcenter()
    }
}

def srcDirs = files(android.sourceSets.main.java.srcDirs)

task coverage(type:JacocoReport, dependsOn: "testDebug") {
    description = "Coverage Report"

    classDirectories = fileTree(
        dir: "$buildDir/intermediates/classes/debug"
    )

    sourceDirectories = srcDirs
    executionData = files("$buildDir/jacoco/testDebug.exec")

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

coveralls {
    jacocoReportPath = "$buildDir/reports/jacoco/coverage/coverage.xml"
    sourceDirs = srcDirs.files.absolutePath
}